configfile: "benchmarks.yaml"

rule all:
    input: 'results.tsv'

rule combine:
    input:
      expand('benchmarks/diversity_{N}_{R}.tsv',
             N=config['context_N'],
             R=range(config['context_R'])),
      expand('benchmarks/neighbors_F_{Nf}_{Rf}_C_{Nc}_{Rc}.tsv',
             Nf=config['focal_N'],
             Rf=range(config['focal_R']),
             Nc=config['context_N'],
             Rc=range(config['context_R']))
    output: 'results.tsv'
    shell: """
    paste <(echo "file") <(head -n 1 $(head -n 1 <(ls {input}))) > {output};
    paste <(ls {input}) <(tail -q -n+2 {input}) >> {output};
    """

rule subset:
    input: lambda w: config[w.type + '_seqs'],
    output: 'subsets/subset_{type}_{N}_{R}.qza'
    shell: """
    qiime genome-sampler sample-random \
      --m-ids-file {input} \
      --p-n {wildcards.N} \
      --o-selection {output}
    """

rule filter:
    input:
        seqs = lambda w: config[w.type + '_seqs'],
        selection = rules.subset.output
    output: 'seqs/seqs_{type}_{N}_{R}.qza'
    shell: """
    qiime feature-table filter-seqs \
      --i-data {input.seqs} \
      --m-metadata-file {input.selection} \
      --o-filtered-data {output}
    """

rule sample_diversity:
    input: 'seqs/seqs_context_{N}_{R}.qza'
    output: 'sel/sel_context_{N}_{R}.qza'
    params:
      percent_id = 0.999
    threads: 9999
    benchmark: "benchmarks/diversity_{N}_{R}.tsv"
    shell: """
    qiime genome-sampler sample-diversity \
      --i-context-seqs {input} \
      --p-percent-id {params.percent_id} \
      --p-n-threads {threads} \
      --o-selection {output}
    """

rule sample_neighbors:
    input:
      focal_seqs = 'seqs/seqs_focal_{Nf}_{Rf}.qza',
      context_seqs = 'seqs/seqs_context_{Nc}_{Rc}.qza',
      locale = config['context_metadata']
    params:
      locale_col = 'division',
      percent_id = 0.9999,
      cluster = 3
    output: 'sel/neighbors_F_{Nf}_{Rf}_C_{Nc}_{Rc}.qza'
    threads: 9999
    benchmark: 'benchmarks/neighbors_F_{Nf}_{Rf}_C_{Nc}_{Rc}.tsv'
    shell: """
    qiime genome-sampler sample-neighbors \
      --i-focal-seqs {input.focal_seqs} \
      --i-context-seqs {input.context_seqs} \
      --m-locale-file {input.locale} \
      --m-locale-column {params.locale_col} \
      --p-percent-id {params.percent_id} \
      --p-samples-per-cluster {params.cluster} \
      --p-n-threads {threads} \
      --o-selection {output}
    """
